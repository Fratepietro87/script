#!/bin/csh
#***********************************************************************
#*
#* IBM AIX CSHELL Script for COMIT Consegne
#*
#* Author: Cavana Carlo 1998
#*
#***********************************************************************

# **********************************************************************
# Actions check
# **********************************************************************
if (`echo $1 | awk '{print length($1)}'` != 3) then  
   echo Prj $1 MUST be 3 chars
   exit 0
endif

# **********************************************************************
# Set Environment Vars
# **********************************************************************
set CCM45_DB = "/usr/CONT_SRV/ccmdb/DB_FIL"
set CCM45_DBlog = "$CCM45_DB/lib/notify/log"
set MIG_ROOT = "/usr/local/MIG_DB_FIL"
set WA_ROOT  = "/usr/CONT_SRV/ccm_wa/DB_FIL"
set DB_Prj   = `echo $1 | awk '{print toupper($1)}'`

setenv CCM_ADDR `ccm start -m -q -nogui -r build_mgr -d $CCM45_DB`
set fnd = `ccm query -t project -n $DB_Prj`
if ( $#fnd != 0) then
   echo " "
   echo Abort: Could not create $DB_Prj. Found $DB_Prj Project into $CCM45_DB Continuus DataBase.
   echo " "
   ccm stop
   exit 0
endif
# MIG env creating...
ccm create -t project $DB_Prj~MIG 
ccm work_area -wa -setpath $WA_ROOT $DB_Prj~MIG
ccm query -t project -name $DB_Prj -v MIG 
ccm attr -c platform -t string -v AIX-DB_FIL @1
ccm attr -m baseline -v "$DB_Prj~MIG" @1
ccm ci -c "Ambiente di migrazione DB_FIL" -p $DB_Prj~MIG -s prep
ccm ci -c "Versione Iniziale" -p $DB_Prj~MIG -source -ss integrate
ccm set role ccm_admin
ccm del `ccm reconf_prop -sh tasks $DB_Prj~MIG -u -f "%objectname"`

########################
# Create Folder di MIG #
########################
ccm set role build_mgr 
set folder_mig = `ccm folder -cr -n "Consegne $DB_Prj migrate in MIG " -w Build_Manager -us All -q`
ccm reconf_prop -add -folders $folder_mig $DB_Prj~MIG
ccm query -t folder -name $folder_mig
ccm attr -c folder_subsys -t string -v $DB_Prj~MIG @1

######################################################
# Create and add task ghost al Folder di MIG         # 
# Il progressivo e' 00001 per evitare omonimie       #
# associazione della dir ver.1 della dir del Progetto#
######################################################
set ver =  00001
  set task_gh = `ccm create_task -s "$DB_Prj$ver~MIG" -platform AIX-DB_FIL -subsystem $DB_Prj | awk '{print ($2)}'`
  ccm query "cvtype='task' and task_subsys='$DB_Prj' and task_synopsis='${DB_Prj}00001~MIG'"
  ccm attr -c comitver -t string -v "00001" @1
  ccm attr -c cod_opzione -t string -v "NoAction" @1
  ccm attr -c resolver -t string -v "cm_sesi" @1
  ccm attr -m status -v "completed" @1

  ccm query "cvtype='dir' and name='$DB_Prj' and version='1'"
  ccm relate -n associated_cv -from task$task_gh~1:task:probtrac -to  @1
  ccm query "cvtype='folder' and folder_subsys='$DB_Prj~MIG'"
  ccm folder -modify -add_task $task_gh @1

# CSU, SQA, PRD env creating...
set Env = `printf "%s\n%s\n%s\n%s" MIG CSU SQA PRD`
set Envnum = `echo $#Env` 
set j = 0
set i = 1
while ($i < $Envnum)
   @ j++
   @ i++
   ccm co -c "Ambiente di integrazione $Env[$i] DB_FIL" -p $DB_Prj~$Env[$j] -platform AIX-DB_FIL -versions $Env[$j]\:$Env[$i]
   ccm work_area -wa -setpath $WA_ROOT $DB_Prj~$Env[$i]
   ccm query -t project -name $DB_Prj -v $Env[$i] 
   ccm attr -m owner -v `echo $Env[$i] |  awk '{print tolower($0)}'`_sesi @1
   ccm attr -m baseline -v "$DB_Prj~$Env[$i]" @1
   ccm set role ccm_admin
   ccm del `ccm reconf_prop -sh tasks $DB_Prj~$Env[$i] -u -f "%objectname"`
   ccm set role build_mgr 
   set folder_id = `ccm folder -cr -n "Consegne $DB_Prj pronte per $Env[$i]" -w Build_Manager -us All -q`
   ccm reconf_prop -add -folders $folder_id $DB_Prj~$Env[$i]
   ccm query -t folder -name $folder_id
   ccm attr -c folder_subsys -t string -v $DB_Prj~$Env[$i] @1
end
# Env created ...
ccm stop
